import org.springframework.boot.gradle.plugin.SpringBootPlugin

apply from: 'deps.gradle'

/**
 * Gradle 依賴配置
 */
buildscript {
    ext {
        springBootVersion = '2.1.1.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }

}

/**
 * 子项目通用的配置
 */
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    // 通用依赖
    dependencies {
        compile 'org.projectlombok:lombok'
        compile 'javax.xml.bind:jaxb-api'
        compile 'com.google.guava:guava'
    }

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:Cairo-SR4'
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }
}

/**
 * 公用的配置
 */
allprojects {
    group = 'in.hocg'
    version = '0.1.1-SNAPSHOT'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url 'http://maven.aliyun.com/nexus/content/groups/public/'
        }
    }
}
